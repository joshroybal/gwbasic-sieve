10 INPUT N%
20 LET SSIZ% = SQR(N%) + 1
30 DIM SIEVE(SSIZ%)
40 GOSUB 120
50 GOSUB 170
60 LET NP% = PSIZ%
70 FOR START% = SSIZ% TO N% STEP SSIZ%
80 GOSUB 340
90 NEXT START%
100 PRINT : PRINT "PI("; N%; ") = "; NP%
110 GOTO 500
120 'initialize sieve segment
130 FOR I% = 1 TO SSIZ%
140 SIEVE(I%) = 1
150 NEXT I%
160 RETURN
170 'compute and count base prime nos.
180 LET PSIZ% = 0
190 SIEVE(1) = 0
200 FOR I% = 2 TO SQR(SSIZ%) + 1
210 FOR J% = I% * I% TO SSIZ% STEP I%
220 SIEVE(J%) = 0
230 NEXT J%
240 NEXT I%
250 FOR I% = 1 TO SSIZ%
260 IF SIEVE(I%) = 1 THEN PRINT I%; : LET PSIZ% = PSIZ% + 1
270 NEXT I%
280 DIM PRIMES(PSIZ%)
290 LET CNT% = 0
300 FOR I% = 1 TO SSIZ%
310 IF (SIEVE(I%) = 1) THEN CNT% = CNT% + 1 : PRIMES(CNT%) = I%
320 NEXT I%
330 RETURN
340 'compute current segment and print primes found in it
350 GOSUB 120
360 FOR I% = 1 TO PSIZ%
370 LET LO% = (START% \ PRIMES(I%)) * PRIMES(I%)
380 IF (LO% < START%) THEN LET LO% = LO% + PRIMES(I%)
390 LET HI% = START% + SSIZ%
400 IF (HI% >= N%) THEN LET HI% = N%
410 FOR J% = LO% TO HI% STEP PRIMES(I%)
420 SIEVE(J% - START%) = 0
430 NEXT J%
440 NEXT I%
450 FOR I% = 1 TO SSIZ%
460 IF (START% + I% > N%) THEN RETURN
470 IF (SIEVE(I%) = 1) THEN PRINT START% + I%; : LET NP% = NP% + 1
480 NEXT I%
490 RETURN
500 END
