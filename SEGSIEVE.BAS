10 INPUT N%
20 LET SSIZ% = SQR(N%) + 1
30 DIM SIEVE(SSIZ%)
40 GOSUB 100
50 GOSUB 150
60 FOR START% = SSIZ% + 1 TO N% STEP SSIZ%
70 GOSUB 320
80 NEXT START%
90 GOTO 480
100 'initialize sieve segment
110 FOR I% = 1 TO SSIZ%
120 SIEVE(I%) = 1
130 NEXT I%
140 RETURN
150 'compute and count base prime nos.
160 LET PSIZ% = 0
170 SIEVE(1) = 0
180 FOR I% = 2 TO SQR(SSIZ%)
190 FOR J% = I% * I% TO SSIZ% STEP I%
200 SIEVE(J%) = 0
210 NEXT J%
220 NEXT I%
230 FOR I% = 2 TO SSIZ%
240 IF SIEVE(I%) = 1 THEN PRINT I%; : LET PSIZ% = PSIZ% + 1
250 NEXT I%
260 DIM PRIMES(PSIZ%)
270 LET CNT% = 0
280 FOR I% = 1 TO SSIZ%
290 IF (SIEVE(I%) = 1) THEN CNT% = CNT% + 1 : PRIMES(CNT%) = I%
300 NEXT I%
310 RETURN
320 'compute current segment and print primes found in it
330 GOSUB 100
340 FOR I% = 1 TO PSIZ%
350 LET LO% = (START% \ PRIMES(I%)) * PRIMES(I%)
360 IF (LO% < START%) THEN LET LO% = LO% + PRIMES(I%)
370 LET HI% = START% + SSIZ%
380 IF (HI% > N%) THEN LET HI% = N%
390 FOR J% = LO% TO HI% STEP PRIMES(I%)
400 SIEVE(J% - START%) = 0
410 NEXT J%
420 NEXT I%
430 FOR I% = 1 TO SSIZ%
440 IF (START% + I% > N%) THEN RETURN
450 IF (SIEVE(I%) = 1) THEN PRINT START% + I%;
460 NEXT I%
470 RETURN
480 END
